buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.1"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
    }
}

group = 'uk.co.automatictester'
version = lightningGradlePluginVersion

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'
apply plugin: 'signing'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'io.codearte.nexus-staging'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }

    useTestNG()
    testLogging {
        events "started", "passed", "skipped", "failed"
    }

    doFirst {
        println "\nIf you execute this task for first time, it may take a good while to run. This is because our tests cover compatibility with different Gradle versions, which may need to be downloaded first. However, this will be handled automatically for you and will happen only once.\n"
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

gradlePlugin {
    testSourceSets sourceSets.integrationTest
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    implementation "uk.co.automatictester:lightning-core:${lightningCoreVersion}"
    integrationTestImplementation 'org.testng:testng:7.1.1'
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId group
            artifactId 'lightning-gradle-plugin'
            version lightningGradlePluginVersion

            from components.java
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (findProperty('ossrhUsername') == null) {
                ext.ossrhUsername = ''
                ext.ossrhPassword = ''
            }

            println "Using OSSRH credentials: ${ossrhUsername}"

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                group group
                name 'lightning-gradle-plugin'
                version lightningGradlePluginVersion
                packaging 'jar'
                description 'Gradle plugin encapsulating Lightning functionality'
                url 'http://automatictester.github.io/lightning'

                scm {
                    connection 'scm:git:git@github.com:automatictester/lightning.git'
                    developerConnection 'scm:git:git@github.com:automatictester/lightning.git'
                    url 'git@github.com:automatictester/lightning.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        name 'Ireneusz Pastusiak'
                        email 'ireneusz.pastusiak@gmail.com'
                        organizationUrl 'https://github.com/automatictester'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

pluginBundle {
    website = 'http://automatictester.github.io/lightning'
    vcsUrl = 'https://github.com/automatictester/lightning'
    description = 'Integrate JMeter non-functional tests with CI/CD server'
    tags = ['jmeter', 'ci', 'cd', 'continuous-integration', 'continuous-delivery', 'performance-testing', 'non-functional-testing']

    plugins {
        LightningPlugin {
            id = 'uk.co.automatictester.lightning'
            displayName = 'Lightning Gradle Plugin'
        }
    }
}